#compdef vimdo
# --------------- ------------------------------------------------------------
#           Name : _vimdo
#       Synopsis : zsh completion for vimdo
#         Author : Zhao Cai <caizhaoff@gmail.com>
#       HomePage : https://github.com/zhaocai/vimdo
#        Version : 0.1
#   Date Created : Sun 07 Apr 2013 03:05:47 PM EDT
#  Last Modified : Sun 07 Apr 2013 03:05:47 PM EDT
#            Tag : [ shell, zsh, completion, vim ]
#      Copyright : Â© 2013 by Zhao Cai,
#                  Released under current GPL license.
# --------------- ------------------------------------------------------------

local curcontext state line ret expl

curcontext="$curcontext"
ret=1

_arguments  -C            \
    '1: :->tasks'          \
    '*: :->args' && ret=0

case $state in
    tasks)
	tasks=( ${(z)"$(_call_program vimdo vimdo autocomplete $words[2,-1])"} )
	_describe -t commands 'vimdo tasks' tasks && ret=0
	;;

    args)
	tasks=( ${(z)"$(_call_program vimdo vimdo autocomplete $words[2,-1])"} )
	if [[ -z "$tasks" ]] ; then
	    _path_files
	else 
	    _describe -t commands 'vimdo tasks' tasks && ret=0
	fi
esac

return ret




